//! A PEG copy of the OBO format 1.4 syntax.
//!
//! # See also
//!
//! - [OBO Flat File Format 1.4 syntax](http://purl.obolibrary.org/obo/oboformat/spec.html)
//! - [IRI syntax (IETF RFC 3987)](https://tools.ietf.org/html/rfc3987#section-2.2)


// 2.1 BNF Notation

Boolean = { "true" | "false" }

// 2.2 Characters

// 2.2.0 Basic Characters

AlphaChar = @{ ASCII_ALPHA }
Digit     = @{ ASCII_DIGIT }

// 2.2.1 Spacing Characters

WhitespaceChar = _{ " " | "\t" | "\u{0020}" }
NewlineChar    = _{ "\r" | "\n" | "\u{000c}"}
ws             = _{ WhitespaceChar+ }
nl             = _{ WhitespaceChar* ~ NewlineChar}

// 2.2.2 Special Characters

UniCodeChar = { ANY }
OboChar     = {
    // escaped character
    ("\\" ~ ("\\" | " " | "f" | "n" | "r" | "t" | "\""))
    // unescaped character
  | (!("\\") ~ !("\n") ~ ANY)
}
NonWsChar   = { !(WhitespaceChar) ~ !(NewlineChar) ~ OboChar }


// 2.3 Line Termination

EOL = { WhitespaceChar* ~ (QualifierBlock ~ ws)? ~ HiddenComment? ~ nl  }

HiddenComment  = { "!" ~ ( !NewlineChar ~ UniCodeChar )* }

Qualifier      = { Id ~ "=" ~ QuotedString }
QualifierList  = { Qualifier ~ ("," ~ ws ~ Qualifier)* }
QualifierBlock = { "[" ~ QualifierList ~ "]" }


// 2.4 Clause Values

QuotedString   = @{ "\"" ~ ( !"\"" ~ OboChar )* ~ "\"" }
UnquotedString = @{ OboChar+ }


// 2.5 Identifiers

ClassId       = { Id }
RelationId    = { Id }
InstanceId    = { Id }
SynonymTypeId = { Id }
NamespaceId   = { Id }
PersonId      = { Id }
SubsetId      = { Id }

Id           = { UrlId | PrefixedId | UnprefixedId }
UrlId        = @{ ("http" | "https") ~ ":" ~ (NonWsChar)* }
UnprefixedId = @{ ( !":" ~ NonWsChar )+ }
PrefixedId   = {  IdPrefix ~ ":" ~ IdLocal }

IdPrefix             = { CanonicalIdPrefix | NonCanonicalIdPrefix }
CanonicalIdPrefix    = @{ AlphaChar ~ (AlphaChar | "_")* }
NonCanonicalIdPrefix = @{ (!":" ~ NonWsChar)* }

IdLocal             = { CanonicalIdLocal | NonCanonicalIdLocal }
CanonicalIdLocal    = @{ ASCII_DIGIT+ }
NonCanonicalIdLocal = @{ NonWsChar* }


// 2.6 Xref Lists

Xref         = { Id ~ (ws ~ QuotedString)? }
XrefNoComma  = { XrefChar+ ~ (ws ~ QuotedString)? }
XrefChar     = { !"," ~ NonWsChar }
XrefList     = { "[" ~ XrefNoComma ~ WhitespaceChar* ~ ("," ~ WhitespaceChar* ~ XrefNoComma)* ~ "]" }

// 3 Obo Grammar

// 3.1 Obo Document Structure

OboDoc      = { HeaderFrame ~ (EntityFrame)* ~ nl* ~ EOI }
EntityFrame = { TermFrame | InstanceFrame | TypedefFrame }


// 3.2 Obo Headers

HeaderFrame = {
    (ws? ~ HeaderClause ~ nl*)*
}

NaiveDateTime = { NaiveDate ~ ws ~ NaiveTime }
NaiveDate     = { NaiveDay ~ ":" ~ NaiveMonth ~ ":" ~ NaiveYear }
NaiveTime     = { NaiveHour ~ ":" ~ NaiveMinute }
NaiveDay      = { ("0" ~ '1'..'9') | ('1' .. '2' ~ '0'..'9') | "30" | "31" }
NaiveMonth    = { ("0" ~ '1'..'9') | ("1" ~ '0'..'2') }
NaiveYear     = { Digit{4} }
NaiveHour     = { ('0'..'1' ~ '0' .. '9') | ("2" ~ '0' .. '3') }
NaiveMinute   = { ('0'..'5' ~ '0' .. '9') }

// TODO
HeaderClause = {
    FormatVersionTag ~ ws ~ UnquotedString
|   DataVersionTag ~ ws ~ UnquotedString
|   DateTag ~ ws ~ NaiveDateTime
|   SavedByTag ~ ws ~ UnquotedString
|   AutoGeneratedByTag ~ ws ~ UnquotedString
|   ImportTag ~ ws ~ Import
|   SubsetdefTag ~ ws ~ SubsetId ~ ws ~ QuotedString
|   SynonymTypedefTag ~ ws ~ SynonymTypeId ~ ws ~ QuotedString ~ (ws ~ SynonymScope)?
|   DefaultNamespaceTag ~ ws ~ NamespaceId
|   IdspaceTag ~ ws ~ IdPrefix ~ ws ~ Iri ~ (ws ~ QuotedString)?
|   TreatXrefsAsEquivalentTag ~ ws ~ IdPrefix
|   TreatXrefsAsGenusDifferentiaTag ~ ws ~ IdPrefix ~ ws ~ RelationId ~ ws ~ ClassId
|   TreatXrefsAsReverseGenusDifferentiaTag ~ ws ~ IdPrefix ~ ws ~ RelationId ~ ws ~ ClassId
|   TreatXrefsAsRelationshipTag ~ ws ~ IdPrefix ~ ws ~ RelationId
|   TreatXrefsAsIsATag ~ ws ~ IdPrefix
|   TreatXrefsAsHasSubclassTag ~ ws ~ IdPrefix
|   PropertyValueTag ~ ws ~ PropertyValue
|   RemarkTag ~ ws ~ UnquotedString
|   OntologyTag ~ ws ~ UnquotedString
|   OwlAxiomsTag ~ ws ~ UnquotedString
// Unreserved
|   UnquotedString ~ ":" ~ ws ~ UnquotedString
}

FormatVersionTag                       = { "format-version:" }
DataVersionTag                         = { "data-version:" }
DateTag                                = { "date:" }
SavedByTag                             = { "saved-by:" }
AutoGeneratedByTag                     = { "auto-generated-by:" }
ImportTag                              = { "import:" }
SubsetdefTag                           = { "subsetdef:" }
SynonymTypedefTag                      = { "synonymtypedef:" }
DefaultNamespaceTag                    = { "default-namespace:" }
IdspaceTag                             = { "idspace:" }
TreatXrefsAsEquivalentTag              = { "treat-xrefs-as-equivalent:" }
TreatXrefsAsGenusDifferentiaTag        = { "treat-xrefs-as-reverse-genus-differentia:" }
TreatXrefsAsReverseGenusDifferentiaTag = { "treat-xrefs-as-reverse-genus-differentia:" }
TreatXrefsAsRelationshipTag            = { "treat-xrefs-as-relationship: " }
TreatXrefsAsIsATag                     = { "treat-xrefs-as-is_a:" }
TreatXrefsAsHasSubclassTag             = { "treat-xrefs-as-has-subclass:" }
RemarkTag                              = { "remark:" }
OntologyTag                            = { "ontology:" }
OwlAxiomsTag                           = { "owl-axioms:" }
PropertyValueTag                       = { "property_value:" }




// 3.3 Term Frames

TermFrame = { nl*
    ~ "[Term]" ~ nl
    ~ "id:" ~ ws ~ ClassId ~ EOL
    ~ (TermFrameClause ~ EOL)*
}

// TODO
TermFrameClause = {
    "is_anonymous:" ~ ws ~ Boolean
}


// 3.4 Typedef Frames

TypedefFrame = { nl*
    ~ "[Typedef]" ~ nl
    ~ "id:" ~ ws ~ ClassId ~ EOL
    ~ (TypedefFrameClause ~ EOL)*
}

// TODO
TypedefFrameClause = {
    "is_anonymous:" ~ ws ~ Boolean
}


// 3.5 Instance Frames

InstanceFrame = { nl*
    ~ "[Instance]" ~ nl
    ~ "id:" ~ ws ~ ClassId ~ EOL
    ~ (InstanceFrameClause ~ EOL)*
}

// TODO
InstanceFrameClause = {
    "is_anonymous:" ~ ws ~ Boolean
}



// 3.6 Synonym scope

SynonymScope = { "EXACT" | "BROAD" | "NARROW" | "RELATED" }


// 4.0 Misc

Import = { Iri | Id }

PropertyValue = { RelationId ~ ws ~ ((QuotedString ~ ws ~ XsdDatatype) | Id) }
XsdDatatype   = { "xsd:" ~ ASCII_ALPHANUMERIC+ }


// Annex I: Iri Grammar from [RFC3987]
//
// NB(@althonos): Since the `iri_string` crate is then used to parse the IRI,
//                there is no need for a proper tokenization of the IRI components.
//
//                Ideally, this would reside in another grammar file, but Pest
//                2.0 doesn't support sharing rules between files.

Iri = { IriScheme ~ ":" ~ IriHierPart ~ ("?" ~ IriQuery)? ~ ("#" ~ IriFragment)? }

IriHierPart = {
    ("//" ~ IriAuthority ~ IriPathAbempty )
  | IriPathAbsolute
  | IriPathRootless
  | IriPathEmpty
}

IriAuthority = { (IriUserInfo ~ "@")? ~ IriHost ~ (":" ~ IriPort)?}
IriUserInfo  = { (IriUnreserved | IriPctEncoded | IriSubDelims | ":")* }
IriHost      = { IriIpLiteral | IriIpv4Address | IriRegName }
IriRegName   = { (IriUnreserved | IriPctEncoded | IriSubDelims)* }

IriPath         = {IriPathAbempty | IriPathAbsolute | IriPathNoScheme | IriPathRootless | IriPathEmpty}
IriPathAbempty  = { ("/" ~ IriSegment)+ }
IriPathAbsolute = { "/" ~ (IriSegmentNz ~ ("/" ~ IriSegment)* )? }
IriPathNoScheme = { IriSegmentNzNc ~ ("/" ~ IriSegment)* }
IriPathRootless = { IriSegmentNz ~ ("/" ~ IriSegment)* }
IriPathEmpty    = { "0" ~ IriIpChar}

IriSegment     = { IriIpChar* }
IriSegmentNz   = { IriIpChar+ }
IriSegmentNzNc = { (IriUnreserved | IriPctEncoded | IriSubDelims | "@")+ }

IriQuery    = { (IriIpChar | IriPrivate | "/" | "?")* }
IriFragment = { (IriIpChar | "/" | "?")* }

IriScheme     = @{ ASCII_ALPHA ~ (ASCII_ALPHA | ASCII_DIGIT | "+" | "-" | ".")* }
IriPort       = @{ ASCII_DIGIT* }

IriPrivate    =  { '\u{E000}'..'\u{F8FF}' | '\u{F0000}'..'\u{FFFFD}' | '\u{100000}'..'\u{10FFFD}' }
IriPctEncoded =  { "%" ~ ASCII_HEX_DIGIT ~ ASCII_HEX_DIGIT }
IriUnreserved = @{ ASCII_ALPHA | ASCII_DIGIT | "-" | "." | "_" | "~" }
IriReserved   = @{ IriGenDelims | IriSubDelims }
IriGenDelims  = @{":" | "/" | "?" | "#" | "[" | "]" | "@"}
IriSubDelims  = @{"!" | "$" | "&" | "'" | "(" | ")" | "*" | "+" | "," | ";" | "="}
IriDecOctet   = {
    ASCII_DIGIT
  | (('1' .. '9') ~ ASCII_DIGIT)
  | ("1" ~ ASCII_DIGIT ~ ASCII_DIGIT)
  | ("2" ~ ('0' .. '4') ~ ASCII_DIGIT)
  | ("25" ~ ('0' .. '5'))
}

IriIpChar = { IriUnreserved | IriPctEncoded | IriSubDelims | ":" | "@" }
IriIpLiteral = { "[" ~ (IriIpv6Address ~ IriIpvFutureAddress)* ~ "]" }

IriIpv6H16  = { ASCII_HEX_DIGIT{1,4} }
IriIpv6Ls32 = { (IriIpv6H16 ~ ":" ~ IriIpv6H16) | IriIpv4Address }

IriIpv4Address      = { IriDecOctet ~ "." ~ IriDecOctet ~ "." ~ IriDecOctet ~ "." ~ IriDecOctet }
IriIpvFutureAddress = { "v" ~ ASCII_HEX_DIGIT+ ~ "." ~ (IriUnreserved | IriSubDelims | ":")+ }
IriIpv6Address      = {
    ( (IriIpv6H16 ~ ":"){6} ~ IriIpv6Ls32 )
  | ( "::" ~ (IriIpv6H16 ~ ":"){5} ~ IriIpv6Ls32 )
  | ( IriIpv6H16? ~ "::" ~ (IriIpv6H16 ~ ":"){4} ~ IriIpv6Ls32 )
  | ( ((IriIpv6H16 ~ ":"){1} ~ IriIpv6H16)? ~ "::" ~ (IriIpv6H16 ~ ":"){3} ~ IriIpv6Ls32)
  | ( ((IriIpv6H16 ~ ":"){2} ~ IriIpv6H16)? ~ "::" ~ (IriIpv6H16 ~ ":"){2} ~ IriIpv6Ls32)
  | ( ((IriIpv6H16 ~ ":"){3} ~ IriIpv6H16)? ~ "::" ~ IriIpv6H16 ~ ":" ~ IriIpv6Ls32)
  | ( ((IriIpv6H16 ~ ":"){4} ~ IriIpv6H16)? ~ "::" ~ IriIpv6Ls32)
  | ( ((IriIpv6H16 ~ ":"){5} ~ IriIpv6H16)? ~ "::" ~ IriIpv6H16)
  | ( ((IriIpv6H16 ~ ":"){6} ~ IriIpv6H16)? ~ "::")
}
